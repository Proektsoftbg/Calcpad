"Double interpolation
'Input parameters
x(j) = spline(j; 2; 4; 8)'- x values
y(i) = spline(i; 3; 9; 27)'- y values
'Output values
R_1(j) = spline(j; 11; 12; 13)' - first row
R_2(j) = spline(j; 21; 22; 23)' - second row
R_3(j) = spline(j; 31; 32; 39)' - third row
T(i; j) = spline(i; R_1(j); R_2(j); R_3(j))' - complete table
'Calculation of interpolation factors
k_x(x) = $Root{x(j) = x @ j = 1 : 3}
k_y(y) = $Root{y(i) = y @ i = 1 : 3}
'The interpolated function
f(x; y) = T(k_x(x); k_y(y))
'Usage
f(2; 3)
f(5; 5)
f(4; 9)
f(8; 27)
'Table
#val
'<!--'i = 0''j = 0'-->
'<table class="bordered">
'<tr><th></th> 
#repeat 3
	'<th><!--'j = j + 1'-->'x(j)'</th>
#loop
'</tr> 
#repeat 3
	'<tr><th><!--'i = i + 1''j = 0'-->'y(i)'</th>
	#repeat 3
		'<td><!--'j = j + 1'-->'T(i; j)'</td>
	#loop
	'</tr>
#loop
'</table>
#equ
PlotHeight = 300
PlotWidth = PlotHeight
'Plot
$Map{f(x; y) @ x = 2 : 8 & y = 3 : 27}