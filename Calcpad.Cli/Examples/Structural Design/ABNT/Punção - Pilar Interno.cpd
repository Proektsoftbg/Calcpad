#Show
"ELU Verificação à punção em lajes para pilares internos<hr/><small>Em conformidade com a <strong>ABNT</strong>: NBR 6118:2014 (Item 19.5)</small>
'<h4>Coeficientes de Ponderação dos materiais</h4>'
'Coeficiente de minoração do aço:     
γ_s = ? 
'Coeficiente de minoração do concreto:
γ_c = ? 
'<h4>Materiais</h4>
'<p><b>Concreto</b></p>
'Resistência característica do concreto à compressão:'
f_ck = ? MPa
#hide
'Conversão de fck para adimensional
f_ck = f_ck/MPa
α_v2 = 1 - f_ck/250
f_ck = f_ck*MPa
#post
'Resistência de projeto do concreto à compressão:'
f_cd = f_ck/γ_c
'Fator de eficiência:'
α_v2
#show
'<h4>Esforços solicitantes</h4>'
F_sd = ? kN
M_sd,x = ? kNm
M_sd,y = ? kNm
'<h4>Dados Geométricos</h4>'
'<p><b>Pilar</b></p>
'Dimensão x do pilar:'hx = ? cm
'Dimensão y do pilar:'hy = ? cm
'<p><b>Laje</b></p>
'Altura da laje:'h = ? cm
'Altura útil da laje para a seção x:'d_x = ? cm
'Altura útil da laje para a seção y:'d_y = ? cm
#Post
'Altura útil da laje:'d = (d_x + d_y)/2|cm
'<hr />
'<h4>Verificação do contorno C:</h4>'
'Perímetro crítico
u = 2*(hx + hy)
'<p class = "ref">[NBR 6118:2014 | 19.5.2.1]</p>
'Tensão solicitante na superfície crítica:
τ_Sd = F_sd/(u*d)|MPa
'<p class="ref">[NBR 6118:2014 | 19.5.3.1]</p>
'Tensão resistente na superfície crítica:
τ_Rd2 = 0.27*α_v2*f_cd|MPa
'Aproveitamento da seção:
τ_Sd/τ_Rd2
'Conclusão da análise:
#if τ_Sd/τ_Rd2 > 1
 '<p style="color:Red;background-color:LavenderBlush"> Reprovado!</p>'
#else
'<p style="color:Blue;background-color:Azure;"> Aprovado!</p>'
#end if
#show
'<hr />
'<h4>Verificação do contorno C´</h4> 
#post
'Perímetro Crítico:
u = 2*(hx + hy) + 4*π*d|cm
'Fator de escala:
k = min(2; 1 + sqrt(20cm/d))
#hide
k = k*MPa
f_ck = f_ck/MPa
#show
'Tensão Normal na seção x:
σ_cp,x = ? MPa
'Tensão Normal na seção y:
σ_cp,y = ? MPa
#post
'Tensão Normal:
σ_cp = (σ_cp,x + σ_cp,y)/2
#show
'Largura da seção x:'hx + 6*d|cm
'Largura da seção y:'hy + 6*d|cm
'Armadura longitudinal na direção x:'
A_sx = ? cm^2
'Armadura longitudinal na direção y:'
A_sy = ? cm^2
#post
'Taxa de armadura longitudinal na seção x:
ρ_x = A_sy/((hx + 6*d)*d_x)
'Taxa de armadura longitudinal na seção y:
ρ_y = A_sx/((hy + 6*d)*d_y)
'Taxa de armadura longitudinal:
ρ = sqrt(ρ_x*ρ_y)
'<p class = "ref">[NBR 6118:2014 | 19.5.3.2]</p>
'Tensão resistente na superfície crítica sem armadura de punção:
τ_Rd1 = 0.13*k*(100*ρ*f_ck)^(1/3) + 0.10*σ_cp
'Dimensão Perpendicular ao Momento:
c_1 = hy
'Dimensão Paralela ao Momento:
c_2 = hx
'Módulo de resistência plástica do perímetro crítico:
W_px = c_1^2/2 + c_1*c_2 + 4*c_2*d + 16*d^2 + 2*π*d*c_1
'Coeficiente que fornece a parcela de Msd transmitida ao pilar por cisalhamento:
c_1/c_2
k_x = 0.1885*ln(c_1/c_2) + 0.5857
'Dimensão Perpendicular ao Momento:
c_1 = hx
'Dimensão Paralela ao Momento:
c_2 = hy
'Módulo de resistência plástica do perímetro crítico:
W_py = c_1^2/2 + c_1*c_2 + 4*c_2*d + 16*d^2 + 2*π*d*c_1
'Coeficiente que fornece a parcela de Msd transmitida ao pilar por cisalhamento:
c_1/c_2
k_y = 0.1885*ln(c_1/c_2) + 0.5857
'<p class = "ref">[NBR 6118:2014 | 19.5.2.2]</p>
'Tensão solicitante na superfície crítica:
#if M_sd,x ≡ 0kNm
τ_Sd = F_sd/(u*d) + k_x*M_sd,y/(W_px*d)|MPa
#else if M_sd,y ≡ 0kNm
τ_Sd = F_sd/(u*d) + k_y*M_sd,x/(W_py*d)|MPa
#else
τ_Sd = F_sd/(u*d) + k_x*M_sd,y/(W_px*d) + k_y*M_sd,x/(W_py*d)|MPa
#end if
'Aproveitamento da seção:
#Post
τ_Sd/τ_Rd1
'Conclusão da análise:
#if τ_Sd/τ_Rd1 > 1
 '<p style="color:Red;background-color:LavenderBlush"> Reprovado!</p>'
#else
'<p style="color:Blue;background-color:Azure;"> Aprovado!</p>'
#end if
#show
'<hr />
'<h4>Verificação do contorno C´ com armadura de punção</h4> 
'Armadura transversal
'<p>Tipo de armadura: 
'<select data-target="reforco">
'<option value=1>Estribo</option > 
'<option value=2>Conector</option>
'</select></p>
'<p id="reforco" style="display:none;">'reforco = ? '</p>
'Bitola do reforço:
φ = ? mm'
'Espaçamento radial: 
s_r = ? cm
'Número de barras em uma linha homotética a C′:
n_r = ? 
'Ângulo de inclinação da armadura transversal:
α = ? °
'Distância da face do pilar até a última linha de conectores:
p = ? cm
#hide
#if reforco ≡ 1
f_max = 300MPa
#else
f_max = 250MPa
#end if
#if (h > 15cm)*(h ≤ 35cm)
f_ywd = 435MPa - ((435MPa - f_max)/(35cm - 15cm)*(35cm - h))
#else if (h ≤ 15cm)
f_ywd = f_max
#else if (h > 15cm)
f_ywd = 435MPa
#end if
#post
'Tensão de escoamento da armadura transversal:
f_ywd
'Área de aço na superfície C´:
A_sw = 0.25*φ^2*π*n_r
'<p class="ref">[NBR 6118:2014 | 19.5.3.3]</p>
'Tensão resistente na superfície crítica C´ em trechos com armadura de punção:
τ_Rd3 = 0.10*k*(100*ρ*f_ck)^(1/3) + 0.10*σ_cp + 1.5*(d/s_r)*((A_sw*f_ywd*sin(α))/(u*d))|MPa
'Aproveitamento da seção:
τ_Sd/τ_Rd3
'Conclusão da análise:
#if τ_Sd/τ_Rd3 > 1
 '<p style="color:Red;background-color:LavenderBlush"> Reprovado!</p>'
#else
'<p style="color:Blue;background-color:Azure;"> Aprovado!</p>'
#end if
#show
'<hr />
'<h4>Verificação do contorno C´´ </h4> 
#post
'Perímetro Crítico:
u = 2*(hx + hy) + 4*π*d + 2*π*p|cm
#show
'Tensão Normal na seção x:
σ_cp,x = ? MPa
'Tensão Normal na seção y:
σ_cp,y = ? MPa
#post
'Tensão Normal:
σ_cp = (σ_cp,x + σ_cp,y)/2
#show
'Largura da seção x:'hx + 6*d + p|cm
'Largura da seção y:'hy + 6*d + p|cm
'Armadura longitudinal na direção x:'
A_sx = ? cm^2
'Armadura longitudinal na direção y:'
A_sy = ? cm^2
#post
'Taxa de armadura longitudinal na seção x:
ρ_x = A_sy/((hx + 6*d + p)*d_x)
'Taxa de armadura longitudinal na seção y:
ρ_y = A_sx/((hy + 6*d + p)*d_y)
'Taxa de armadura longitudinal:
ρ = sqrt(ρ_x*ρ_y)
'<p class = "ref">[NBR 6118:2014 | 19.5.3.2]</p>
'Tensão resistente na superfície crítica sem armadura de punção:
τ_Rd1 = 0.13*k*(100*ρ*f_ck)^(1/3) + 0.10*σ_cp
'Dimensão Perpendicular ao Momento:
c_1 = hy
'Dimensão Paralela ao Momento:
c_2 = hx
'Módulo de resistência plástica do perímetro crítico:
W_px = c_1^2/2 + c_1*c_2 + 4*c_2*d + 16*d^2 + 2*π*d*c_1 + 2*c_2*p + 4*p^2 + π*c_1*p
'Coeficiente que fornece a parcela de Msd transmitida ao pilar por cisalhamento:
c_1/c_2
k_x = 0.1885*ln(c_1/c_2) + 0.5857
'Dimensão Perpendicular ao Momento:
c_1 = hx
'Dimensão Paralela ao Momento:
c_2 = hy
'Módulo de resistência plástica do perímetro crítico:
W_py = c_1^2/2 + c_1*c_2 + 4*c_2*d + 16*d^2 + 2*π*d*c_1 + 2*c_2*p + 4*p^2 + π*c_1*p
'Coeficiente que fornece a parcela de Msd transmitida ao pilar por cisalhamento:
c_1/c_2
k_y = 0.1885*ln(c_1/c_2) + 0.5857
'<p class = "ref">[NBR 6118:2014 | 19.5.2.2]</p>
'Tensão solicitante na superfície crítica:
#if M_sd,x ≡ 0kNm
τ_Sd = F_sd/(u*d) + k_x*M_sd,y/(W_px*d)|MPa
#else if M_sd,y ≡ 0kNm
τ_Sd = F_sd/(u*d) + k_y*M_sd,x/(W_py*d)|MPa
#else
τ_Sd = F_sd/(u*d) + k_x*M_sd,y/(W_px*d) + k_y*M_sd,x/(W_py*d)|MPa
#end if
'Aproveitamento da seção:
τ_Sd/τ_Rd1
'Conclusão da análise:
#if τ_Sd/τ_Rd1 > 1
 '<p style="color:Red;background-color:LavenderBlush"> Reprovado!</p>'
#else
'<p style="color:Blue;background-color:Azure;"> Aprovado!</p>'
#end if
