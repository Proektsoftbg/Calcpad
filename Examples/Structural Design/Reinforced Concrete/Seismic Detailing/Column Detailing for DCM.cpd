"Column Detailing for Ductility Class DCM<hr/><small>According to <strong>Eurocode</strong>: EN 1998-1</small>
'<div style="max-width:180mm">
'<img class="side" style="width:220pt;" src="https://calcpad.eu/media/structures/rc/detailing/column-section.png" alt="column-section.png">
'<p><b>Column dimensions</b></p>
'Cross section -'b_c = ?'mm,'h_c = ?'mm
'Clear height -'l_cl = ?'mm
'Cross section area -'A_c = b_c*h_c'mm²
'Maximum seismic axial load -'N_Ed = ?'kN

'<p class="ref">[EN 1992-1-1, <a target="_blank" href="https://calcpad.eu/reference/concrete-table.html">Table 3.1</a>]</p>
'<p><b>Concrete</b></p>
'Characteristic compressive cylinder strength -'f_ck = ?'MPa
'Partial safety factor -'γ_c = 1.5','α_ct = 1','α_cc = ?
'<img class="side" style="width:130pt;" src="https://calcpad.eu/media/structures/rc/detailing/column-view.png" alt="column-view.png">
#post
'Mean value of axial tensile strength
f_ctm = 0.30*f_ck^(2/3)'MPa 
'Characteristic axial tensile strength
f_ctk_005 = 0.7*f_ctm'MPa
'Design compressive cylinder strength
f_cd = α_cc*f_ck/γ_c'MPa
#show

'<p><b>Longitudinal reinforcement</b></p>
'<p id="C" style="display:none;">'C = ?'</p>
'Characteristic yield strength -'f_yk = ?'MPa
#pre
'<p>Steel class - <select data-target="C"> 
'<option value="0"> Class B </option>
'<option value="1"> Class C </option>
'</select></p>
#post
#val
#if C ≡ 0
	'Selected steel class <b>B'f_yk'B</b>
#else if C ≡ 1
	'Selected steel class <b>B'f_yk'C</b>
#else
	'<p class="err">Invalid steel class!</p>
#end if
#equ
'Partial safety factor for steel -'γ_s = 1.15
'Design yield strength -'f_yd = f_yk/γ_s'MPa
'Modulus of elasticity -'E_s = 200000'MPa
#show
'Bar diameter -'d_bL = ?'mm
#post
'<p class="ref">[BS EN 1992-1-1, § 9.5.2 (1)/NA.1]</p>
'Minimum bar diameter -'d_bL_min = 12'mm
#if d_bL < d_bL_min
	'<p class="err"> Bar diameter is less than the minimum:'d_bL'mm &lt;'d_bL_min'mm</p>
#end if
#show
'Bar count -'n_b = ?
'Bar count along "<i>h</i><sub>c</sub>" -'n_b1 = ?
#post
'Bar count along "<i>b</i><sub>c</sub>" -'n_b2 = n_b/2 - n_b1 + 2
#if n_b < 4
	'<p class="err">Bar count is'n_b'&lt; 4</p>
#else if (n_b1 < 3) + (n_b2 < 3)
	'<p class="ref">[§ 5.4.3.2.2 (2)]</p>
	'<p class="err">Minimum one intermediate bar at each side is required.</p>
#end if 
'Reinforcement area
A_s1 = π*d_bL^2/4'mm²
A_s = n_b*A_s1'mm²
'Reinforcement ratio
ρ_L = A_s/A_c
'<p class="ref">[§ 5.4.3.2.2 (1)]</p>
#if ρ_L < 0.01
	'<p class="err">Reinforcement ratio is less than minimum:'ρ_L'&lt; 0.01</p>
#else if ρ_L > 0.04
	'<p class="err">Reinforcement ratio is greater than maximum:'ρ_L'&gt; 0.04</p>
#else 
	'Design check: 0.01 ≤'ρ_L'≤ 0.04. The check is satisfied!
#end if

'<p class="ref">[EN 1992-1-1, § 9.5.1 (1)]</p>
'<p><b>Column dimensions check</b></p>
#if h_c/b_c > 4
	'<p class="err">'h_c/b_c'&gt; 4</p>
#else
	h_c/b_c'≤ 4. The check is satisfied!
#end if
'<p class="ref">[§ 5.4.3.2.1 (3)]</p>
'Check for normalized axial load
ν_d = N_Ed/(A_c*f_cd)*1000
#if ν_d > 0.65
	'<p class="err">'ν_d'&gt; 0.65. The check is NOT satisfied!</p>
#else
	ν_d'≤ 0.65. The check is satisfied!
#end if

#if l_cl/h_c < 3
	'<p class="ref">[§ 5.4.3.2.2 (5)]</p>
	'<p><b>Critical region length</b></p>
	l_cr = l_cl'mm - when'l_cl/h_c'&lt; 3 total height is critical region.
#else
	'<p class="ref">[§ 5.4.3.2.2 (4)]</p>
	'<p><b>Critical region length</b></p>
	l_cr = max(h_c;max(l_cl/6;450))'mm
#end if
'Design anchorage length
η_1 = 1'- when good conditions are provided
#if d_bL ≤ 32
	η_2 = 1'- за'd_bL'≤ 32 mm
#else
	η_2 = (132 - d_bL)/100'- за'd_bL'> 32 mm
#end if
f_ctd = α_ct*f_ctk_005/γ_c'MPa
'<p class="ref">[EN 1992-1-1, § 8.4.2 (2)]</p>
f_bd = 2.25*η_1*η_2*f_ctd'MPa
σ_sd = f_yd'MPa
'<p class="ref">[EN 1992-1-1, § 8.4.3 (2)]</p>
l_b_rqd = d_bL/4*σ_sd/f_bd'mm
'<p class="ref">[EN 1992-1-1, Table 8.2]</p>
α_1 = 1','α_2 = 1','α_3 = 1','α_5 = 1','α_6 = 1.5
'<p class="ref">[EN 1992-1-1, § 8.7.3 (1)]</p>
l_0_ = α_1*α_2*α_3*α_5*α_6*l_b_rqd'mm
l_0_min = max(0.3*α_6*l_b_rqd;max(15*d_bL;200))'mm
l_0 = round(max(l_0_;l_0_min))'mm
'Mid zone length
l_1 = max(0;l_cl - l_cr - l_0)'mm
#show

'<p><b>Transverse reinforcement</b></p>
'Link diameter -'d_bw = ?'mm
'Characteristic yield strength -'f_ywk = ?'MPa
#post
'Design yield strength -'f_ywd = f_ywk/γ_s'MPa
'Links concrete cover -'c = 40'mm
'<p class="ref">[EN 1992-1-1, § 9.5.3 (1)]</p>
'Minimum diameter -'d_bw_min = max(6;0.25*d_bL)'mm
#if d_bw < d_bw_min
	'<p class="err">Link diameter is less than minimum:</p>
	'<p class="err">'d_bw'&lt;'d_bw_min'mm</p>
#else
	'Link diameter check:
	d_bw'≥'d_bw_min'mm. The check is satisfied!
#end if

'Confined core dimensions (between centerlines of links)
b_0 = b_c - (d_bw + 2*c)'mm
h_0 = h_c - (d_bw + 2*c)'mm
'Maximum bar spacing
d_b1 = (h_c - 2*(d_bw + c) - d_bL)/(n_b1 - 1)'mm
d_b2 = (b_c - 2*(d_bw + c) - d_bL)/(n_b2 - 1)'mm
'Maximum distance between consecutive longitudinal bars engaged by links
'<p class="ref">[§ 5.4.3.2.2 (11), b)]</p>
d_h_max = 200'mm
'Distance between bars engaged by links
n_h1 = max(floor(d_h_max/d_b1);1)
n_h2 = max(floor(d_h_max/d_b2);1)
'Distance between bars engaged by links
d_h1 = n_h1*d_b1
d_h2 = n_h2*d_b2
#if d_h1 > d_h_max
	'<p class="err">Distance is greater than maximum:'d_h1'mm >'d_h_max'mm</p>
#else if '<!--'d_h2 > d_h_max'-->
	'<p class="err">Distance is greater than maximum:'d_h2'mm >'d_h_max'mm</p>
#end if
'Distance between bars engaged by links
n_h1 = round((n_b1 - 1)*d_b1/d_h1)
n_h2 = round((n_b2 - 1)*d_b2/d_h2)

'Link spacing in critical regions
'<p class="ref">[§ 5.4.3.2.2 (11), a)]</p>
s_cr = min(b_0/2;min(8*d_bL;175))'mm
'Link spacing at mid zone
'<p class="ref">[EN 1992-1-1, § 9.5.3 (3)]</p>
s = min(b_c;min(20*d_bL;400))'mm
'Link spacing in lap zone
'<p class="ref">[§ 5.6.3 (3), c)]</p>
s_l = min(100;b_c/4)'mm

'<b>Transverse reinforcement in the lap zone</b>
'Required area of one leg
'<p class="ref">[§ 5.6.3 (4)]</p>
A_st = s_l*(d_bL/50)*(f_yd/f_ywd)'mm²
'Provided area of one leg
A_sw1 = π*d_bw^2/4'mm²
#if A_sw1 < A_st
	'<p class="err">Insufficient area of one leg in the lap zone:'A_sw1'mm² &lt;'A_st'mm²</p>
#else
	'Design check:'A_sw1'mm² ≥'A_st'mm². The condition is satisfied!
#end if
#if d_bL > 20
	'Check for bar diameters > 20 mm:
	'Number of legs in the outer 1/3 of lap zone
	n_w = round(2*l_0/3/s_l)
	'Total area of legs in the outer 1/3 of lap zone
	ΣA_sw = A_sw1*n_w
	'<p class="ref">[EN 1992-1-1, § 8.7.4.1 (3)]</p>
	#if ΣA_sw < A_sw1*n_w
		'<p class="err">Insufficient transverse reinforcement area in the lap zone:'ΣA_sw'mm² &lt;'A_s1'mm²</p>
	#else
		'Design check:'ΣA_sw'mm² ≥'A_s1'mm²
	#end if
	'Additional link is required for compressed bars
	'<p class="ref">[EN 1992-1-1, § 8.7.4.2 (1)]</p>
	'at'4*d_bL'mm from the end of the lap zone
#end if

'<b>Calculation of link count</b>
'In the lap zone -'n_w_l = round(l_0/s_l)
'In the middle zone -'n_w_1 = round(l_1/s)
#if n_w_1 ≡ 0
	l_cr_top = max(l_cl - l_0;0)
	'In critical region -'n_w_cr = round(l_cr_top/s_cr)
#else
	'In critical region -'n_w_cr = round(l_cr/s_cr)
#end if
'Total number of shear links -'n_w = n_w_l + n_w_1 + n_w_cr

'<p><b>Detailing for local ductility in the critical region at column base</b></p>
'Total length of confining links
Σl_i = (n_h1 + 1)*b_0 + (n_h2 + 1)*h_0
'Mechanical volumetric ratio of confining links within the critical region
ω_d = (A_sw1*Σl_i)/(b_0*h_0*min(s_cr;s_l))*f_ywd/f_cd
'<p class="ref">[§ 5.4.3.2.2 (8)]</p>
'Minimum value is 0.08.
#if ω_d < 0.08
	'<p class="err">Design check:'ω_d'&lt;'0.08'.Mechanical volumetric ratio is less than minimum.</p>
#else
	'Design check:'ω_d'&ge;'0.08'. The condition is satisfied!
#end if
'Sum of the squares of the distances between consecutive engaged bars
Σb2_i = 2*(n_h1*d_h1^2 + n_h2*d_h2^2)
'Confinement effectiveness factors for bars and links
α_n = 1 - Σb2_i/(6*b_0*h_0)
α_s = (1 - s_cr/(2*b_0))*(1 - s_cr/(2*h_0))
α = α_n*α_s
#show
'<p><b>Analysis results</b></p>
'Fundamental period of first vibration mode -'T_1 = ?'s
'Upper limit period of constant spectral acceleration -'T_C = ?'s
'Basic behavior factor value -'q_0 = ?
#post
'<p><b>Curvature ductility factor</b></p>
'<p class="ref">[§ 5.2.3.4 (3)]</p>
#if T_1 ≥ T_C
	μ_Φ = 2*q_0 - 1'- for T<sub>1</sub> ≥ T<sub>C</sub>
#else
	μ_Φ = 1 + 2*(q_0 - 1)*T_C/T_1'- for T<sub>1</sub> &lt; T<sub>C</sub>
#end if
#if C ≡ 0
	'<!--'μ_Φ = 1.5*μ_Φ'-->
	'<p class="ref">[§ 5.2.3.4 (4)]</p>
	'For steel class B, ductility factor is increased by 05% -'μ_Φ
#end if
'Design value of steel yield strain - 'ε_sy_d = f_yd/E_s
'<p class="ref">[§ 5.4.3.2.2 (8)]</p>
'Design check: <i>αω</i><sub>d</sub> &ge; <i>αω</i><sub>d_min</sub> = 30·<i>μ</i><sub>Φ</sub>·<i>ν</i><sub>d</sub>·<i>ε</i><sub>sy_d</sub>·<i>b</i><sub>c</sub>/<i>b</i><sub>0</sub> – 0.035
αω_d = α*ω_d
αω_d_min = 30*μ_Φ*ν_d*ε_sy_d*b_c/b_0 - 0.035
#if αω_d < αω_d_min
	'<p class="err">The required curvature ductility is NOT provided:'αω_d'&lt;'αω_d_min'</p>
#else
	'The required curvature ductility is provided:'αω_d'≥'αω_d_min
#end if
'NOTE: All references are according to EN 1998-1, unless noted otherwise.
#show
'</div>500	500	2850	983.8	25	1	0	500	28	12	4	10	500	0.55	0.6	3.9